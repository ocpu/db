plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
    id 'org.jetbrains.dokka' version '0.9.16'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'maven-publish'
    id 'signing'
}

group 'io.opencubes'
version '0.3.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
//    implementation "mysql:mysql-connector-java:8.0.11"
//    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.25.2'
//    implementation group: 'com.microsoft.sqlserver', name: 'mssql-jdbc', version: '7.2.2.jre8'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenKotlin(MavenPublication) {
            artifactId = archivesBaseName
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = 'A super charged database connection with a model/active record'
                url = "https://github.com/ocpu/db#readme"
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'ocpu'
                        name = 'Martin HÃ¶vre'
                        email = 'martin.hovre@opencubes.io'
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ocpu/db.git"
                    developerConnection = "scm:git:ssh://github.com:ocpu/db.git"
                    url = "http://github.com/ocpu/db/tree/master"
                }
            }
        }
    }
    repositories {
        maven {
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenKotlin
}

bintray {
    user = project.hasProperty('bintrayUser') ? bintrayUser : ""
    key = project.hasProperty('bintrayKey') ? bintrayKey : ""
    publications = ['mavenKotlin']
    pkg {
        repo = 'maven'
        name = project.name
        websiteUrl = "https://github.com/ocpu/$project.name"
        issueTrackerUrl = "https://github.com/ocpu/$name/issues"
        vcsUrl = "https://github.com/ocpu/${project.name}.git"
        licenses = ['MIT']
//        publish = true
        githubRepo = 'ocpu/db'
        version {
            name = project.version
            vcsTag = project.version
            gpg {
                sign = true
                passphrase = hasProperty("signPassword") ? signPassword : ""
            }
        }
    }
}
